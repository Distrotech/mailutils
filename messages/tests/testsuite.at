# This file is part of GNU Mailutils. -*- Autotest -*-
# Copyright (C) 2007-2012 Free Software Foundation, Inc.
#
# GNU Mailutils is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 3, or (at
# your option) any later version.
#
# GNU Mailutils is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with GNU Mailutils.  If not, see <http://www.gnu.org/licenses/>.

m4_include([testsuite.inc])

dnl ------------------------------------------------------------
dnl MSG_MAILBOX  -- mailbox used for testing
m4_define([MSG_MAILBOX],[mbox1])

dnl ------------------------------------------------------------
dnl MSG_OPTIONS  -- default options for frm
m4_define([MSG_OPTIONS],[--no-site --no-user])

dnl ------------------------------------------------------------
dnl MSGTEST(DESCR, KW, CMDLINE, [STATUS = `0'], [STDOUT = `'],
dnl         [STDERR = `'], [RUN-IF-FAIL], [RUN-IF-PASS])
dnl
m4_define([MSGTEST],[
AT_SETUP([$1])
AT_KEYWORDS([messages $2])
AT_CHECK([
MAIL=$abs_top_srcdir/testsuite/spool/MSG_MAILBOX
FOLDER=$MAIL
export MAIL FOLDER
messages MSG_OPTIONS $3 | sed 's|in /.*/|in |'],
m4_shift(m4_shift(m4_shift($@))))
AT_CLEANUP])

AT_INIT

AT_TESTED([messages])

MUT_VERSION(messages)

MSGTEST([messages],[messages00],
[],
[0],
[Number of messages in mbox1: 5
])

MSGTEST([messages -q],[messages01 messages-q],
[-q],
[0],
[5
])

MSGTEST([messages 2],[messages02],
[--set ":mailbox:folder=$abs_top_srcdir/testsuite/spool" +teaparty.mbox],
[0],
[Number of messages in teaparty.mbox: 95
])

